apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

task accessToken {
    def tokenFile = new File("app/src/main/res/values/developer-config.xml")
    if (!tokenFile.exists()) {
        String tokenFileContents = "<?xml version=\"1.0\" encoding=\"utf-8\"?>\n" +
                "<resources>\n" +
                "    <string name=\"mapbox_access_token\">" + "$System.env.MAPBOX_ACCESS_TOKEN" + "</string>\n" +
                "</resources>"

        if (tokenFileContents == null) {
            throw new InvalidUserDataException("You must set the MAPBOX_ACCESS_TOKEN environment variable.")
        }
        tokenFile.write(tokenFileContents)
    }
}

gradle.projectsEvaluated {
    preBuild.dependsOn('accessToken')
}

repositories {
    mavenCentral()
    maven {
        url "https://raw.github.com/platypusllc/core/mvn-repo/"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    playAccountConfigs {
        release {
            jsonFile = file(System.getenv("ANDROID_PLAY_JSON_FILE") ?: ".")
        }
    }
    signingConfigs {
        release {
            keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS") ?: "EXAMPLE_ALIAS"
            keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD") ?: "EXAMPLE_PASSWORD"
            storeFile file(System.getenv("ANDROID_RELEASE_STORE_FILE") ?: ".")
            storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD") ?: "EXAMPLE_PASSWORD"
            v2SigningEnabled true
        }
    }
    defaultConfig {
        applicationId "com.platypus.android.tablet"
        minSdkVersion 21
        targetSdkVersion 25
        multiDexEnabled true
        playAccountConfig = playAccountConfigs.release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig getSigningConfig()
        }
    }
    dexOptions {
        javaMaxHeapSize '4g'
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        // See: https://guides.codepath.com/android/Setting-up-Travis-CI
        preDexLibraries = preDexEnabled && !travisBuild
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

play {
    track = 'alpha'
    untrackOld = true
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.google.firebase:firebase-crash:9.4.0'
    compile ('com.mapbox.mapboxsdk:mapbox-android-sdk:4.2.0@aar') {
        transitive = true
    }
    compile ('org.jscience:jscience:4.3.+') {
        exclude group: 'org.javolution', module: 'javolution'
    }
    compile group: 'com.platypus.crw', name: 'platypus-core', version: '+', changing: true
}

// Google Services are required to support Firebase.
apply plugin: 'com.google.gms.google-services'
